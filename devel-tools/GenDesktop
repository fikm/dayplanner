#!/usr/bin/perl
# GenDesktop
# Generates the .desktop file for Day Planner
# $Id$
# Copyright (C) Eskild Hustvedt 2006
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;			# Force strict coding
use warnings;			# Tell perl to warn about things
use Locale::gettext;		# Allow the program to be translated
use FindBin;			# So that we can detect stuff at runtime
use POSIX;
use utf8;
use Fatal qw/open close/;

sub SetLocale {
	foreach(keys(%ENV)) {
		next unless /^(LC|.*LANG)/;
		$ENV{$_} = $_[0];
	}
	foreach(qw(LANG LC_CTYPE LC_NUMERIC LC_TIME LC_COLLATE LC_MONETARY LC_MESSAGES LC_PAPER LC_NAME LC_ADDRESS LC_TELEPHONE LC_MEASUREMENT LC_IDENTIFICATION LC_ALL))
	{
		$ENV{$_} = $_[0];
	}
	foreach(&POSIX::LC_CTYPE, &POSIX::LC_NUMERIC, &POSIX::LC_TIME, &POSIX::LC_COLLATE, &POSIX::LC_MONETARY, &POSIX::LC_MESSAGES, &POSIX::LC_ALL)
	{
		setlocale($_,$_[0]);
	}
	textdomain("dayplanner");
	bindtextdomain("dayplanner", "./");
	#system('perl','-MLocale::gettext','-Mlocale','-e','$g = Locale::gettext->domain_raw("dayplanner");$g->codeset("UTF-8");print $g->get("Day Planner")."\n";');
	my $GettextObj = Locale::gettext->domain_raw('dayplanner');	# Set the gettext domain
	$GettextObj->codeset('UTF-8');
	return($GettextObj);
}

my %Languages;

my($BinDir, $IconsDir) = @ARGV;
my $Icon;
my $NoErrorForce;

unless(defined($BinDir)) {
	die("Usage: $0 [DIRECTORY CONTAINING THE dayplanner BINARY] [ICONS DIR]\n The binary dir is mandatory, icons dir optional.");
}

if(defined($IconsDir)) {
	$Icon = "$IconsDir/dayplanner-48x48.png";
} else {
	$Icon = "dayplanner.png";
}

# Set dirs
chdir($FindBin::RealBin);
chdir("..");
my %I18nCount;

print "Running ./devel-tools/BuildLocale\n";
system("./devel-tools/BuildLocale");

print "Generating .desktop...:\n";
chdir("locale");
foreach(<*>) {
	if(-d $_ and not -l $_) {
		my $Gettext = SetLocale($_);
		$Languages{$_} = $Gettext->get("Day Planner");
		if($Languages{$_} eq "Day Planner") {
			delete($Languages{$_});
			print "$_: No valid translation of \"Day Planner\" found\n";
		} else {
			print "$_: OK\n";
			$I18nCount{$Languages{$_}} += 1;
		}
	}
}
# Verify count
foreach(keys(%Languages))
{
	if ($I18nCount{$Languages{$_}} > 2)
	{
		if(defined($ENV{DP_FORCE_GENDESKTOP}) and $ENV{DP_FORCE_GENDESKTOP})
		{
			$NoErrorForce = 1;
			last;
		}
		else
		{
			die("GenDesktop: Translation count for '$Languages{$_}' is more than two ($I18nCount{$Languages{$_}}). Something is very wrong.\nYou are probably missing a few locales. Refusing to continue\nIf you wish to suppress this error and continue anyway, set the env var DP_FORCE_GENDESKTOP to any true value.\n");
		}
	}
}
# Create the file
chdir("..");
print "Writing...";
open(my $Desktop, ">", "./doc/dayplanner.desktop");
binmode($Desktop, ':utf8');
if ($NoErrorForce)
{
	use Sys::Hostname;
	print $Desktop "# File forced to be generated even though language counts were invalid\n";
	print $Desktop "# Force on ". scalar(localtime()) . " by $</$> on " .hostname()."\n";
	print $Desktop "# Counts:";
	foreach(keys(%I18nCount))
	{
		print $Desktop " $_ = $I18nCount{$_}";
	}
	print $Desktop "\n";
}
print ".";
print $Desktop "[Desktop Entry]\n";
print $Desktop "Version=1.0\n";
print $Desktop "Encoding=UTF-8\n";
print $Desktop "MimeType=text/calendar\n";
print $Desktop "Categories=X-MandrivaLinux-Office-TimeManagement;Office;Calendar;GTK;GNOME;\n";
if($BinDir eq '.') {
	print $Desktop "Exec=dayplanner\n";
} else {
	print $Desktop "Exec=$BinDir/dayplanner\n";
}
print $Desktop "Icon=$Icon\n";
print $Desktop "Name=Day Planner\n";
print $Desktop "StartupNotify=false\n";
print $Desktop "Terminal=false\n";
print $Desktop "Type=Application\n";
print ".";
foreach(keys(%Languages))
{
	print $Desktop "Name[$_]=$Languages{$_}\n";
}
print ".";
close($Desktop);
print "done\n";
print "Wrote ./doc/dayplanner.desktop\n";
